plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.116'
    id 'idea'
}

def pamTargetVersion = (project.findProperty('pam_target') ?: '1.21.1').toString()
def pamVersionFile = project.layout.projectDirectory.file("versions/${pamTargetVersion}.properties").asFile
if (!pamVersionFile.exists()) {
    throw new GradleException("Missing versions/${pamTargetVersion}.properties for pam_target")
}

def pamVersionProps = new Properties()
pamVersionFile.withReader('UTF-8') { pamVersionProps.load(it) }
pamVersionProps.forEach { key, value ->
    project.extensions.extraProperties.set(key.toString(), value.toString())
}

logger.lifecycle("[PAM] Using profile ${pamTargetVersion} (Minecraft ${project.minecraft_version}, NeoForge ${project.neo_version})")

def commonJavaDir = file("../common/src/main/java")
def commonResourcesDir = file("../common/src/main/resources")

sourceSets {
    main {
        if (commonJavaDir.exists()) {
            java.srcDir(commonJavaDir)
        }
        if (commonResourcesDir.exists()) {
            resources.srcDir(commonResourcesDir)
        }
    }
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    // Add repositories here if required by additional dependencies.
}

base {
    // Build output name: playerattributemanagement-<pam_target>-<mod_version>.jar
    archivesName = "playerattributemanagement-${pamTargetVersion}"
}

// Mojang ships Java 21 to end users in 1.21.x, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = 'gameTestServer'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // Example optional mod dependency with JEI:
    // compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    // compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    // localRuntime "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"
}

var generateModMetadata = tasks.register('generateModMetadata', ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from 'src/main/templates'
    into 'build/generated/sources/modMetadata'
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
